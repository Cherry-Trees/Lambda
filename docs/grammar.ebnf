statement       = assignment
                | expression ;

assignment      = name , { name } , "=" , expression ;

expression      = expression , "||" , expression
                | and ;

and             = and , "&&" , and
                | equality ;
                
equality        = equality , ( "==" | "!=" ) , equality
                | "!" , equality
                | relation ;

relation        = relation , ( "<" | "<=" | ">" | ">=" ) , relation
                | addition ;

addition        = addition , ( "+"  | "-" ) , addition
                | multiplication ;
    
multiplication  = multiplication , ( "*" | "/" | "%" ) , multiplication
                | "-" , multiplication
                | term ;

term            = literal
                | variable
                | application
                | ifthenelse
                | "(" , expression , ")" ;

variable        = name , [ "@" , integer ] ;

literal         = integer
                | boolean
                | lambda ;

application     = term , term ;

ifthenelse      = "if" , expression , "then" , expression , "else" , expression ;

integer         = digit , { digit } ;

boolean         = "True" 
                | "False" ;

lambda          = "\\" , name , "->" , expression ;

name            = lower , { alphanumeric }
                | "_" ;

alphanumeric    = lower
                | upper
                | digit ;

lower           = "a" | "b" | "c" | "d" | "e" | "f" | "g" 
                | "h" | "i" | "j" | "k" | "l" | "m" | "n" 
                | "o" | "p" | "q" | "r" | "s" | "t" | "u" 
                | "v" | "w" | "x" | "y" | "z" ;

upper           = "A" | "B" | "C" | "D" | "E" | "F" | "G"
                | "H" | "I" | "J" | "K" | "L" | "M" | "N"
                | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
                | "V" | "W" | "X" | "Y" | "Z" ;

digit           = "0" | "1" | "2" | "3" | "4" | "5" | "6" 
                | "7" | "8" | "9" ;
